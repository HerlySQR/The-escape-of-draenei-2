package AnimalForce

import AbilityData
import PseudoWindWalk

public constant ANIMAL_FORCE_NL = compiletime(ABIL_ID_GEN.next())
public constant ANIMAL_FORCE_L = compiletime(ABIL_ID_GEN.next())
public constant ANIMAL_FORCE_S = compiletime(ABIL_ID_GEN.next())

constant ANIMAL_FORCE_BUFF = compiletime(BUFF_ID_GEN.next())

// General
let SPEED = 1.75
let DURATION = 8.
let CD = 45.
let MANA = 100
// Not Leveling
let EXTRA_DMG_NL = 100.
// Leveling
let EXTRA_DMG_L = [150., 225.]

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        let data = abilityCond(ANIMAL_FORCE_NL, ANIMAL_FORCE_L, ANIMAL_FORCE_S)
        if data.check
            GetSpellAbilityUnit().startWindWalk(ANIMAL_FORCE_BUFF)
    end)
    DamageEvent.addUnreducedListener(() -> begin
        let source = DamageEvent.getSource()
        if source.hasAbility(ANIMAL_FORCE_BUFF) and DamageEvent.isSpell()
            if whatMode[5] == NOT_LEVELING
                let level = max(source.getAbilityLevel(ANIMAL_FORCE_NL), source.getAbilityLevel(ANIMAL_FORCE_S))
                if level > 0
                    DamageEvent.addAmount(EXTRA_DMG_NL)
            else if whatMode[5] == LEVELING
                let level = max(source.getAbilityLevel(ANIMAL_FORCE_L), source.getAbilityLevel(ANIMAL_FORCE_S) - 1)
                if level > 0
                    DamageEvent.addAmount(EXTRA_DMG_L[level - 1])
    end)

function AbilityDefinitionBerserk.general()
    this
        ..registerTooltipGenerator(new AbilityTooltipGenerator("Increase the speed of the salamnder in " + (SPEED * 100).toString(0) + "% and he can go through to the units for 8 seconds and every spell he cast deal extra damage."))
        ..presetBuffs(lvl -> ANIMAL_FORCE_BUFF.toRawCode())
        ..setEffectSound("StampedeCast")
        ..presetDurationHero(lvl -> DURATION)
        ..presetDurationNormal(lvl -> DURATION)
        ..presetDamageTakenIncrease(lvl -> 0.)
        ..presetAttackSpeedIncrease(lvl -> 0.)
        ..presetMovementSpeedIncrease(lvl -> SPEED)
        ..tooltipStartListen()
        ..addTooltipProperty("Duration", lvl -> DURATION)
        ..presetCooldown(lvl -> CD)
        ..presetManaCost(lvl -> MANA)
        // Audiovisual
        ..presetIcon(Icons.bTNStampede)
        // Tooltip
        ..presetHotkey("R")
        ..setName("Animal Force")
        ..addTooltipProperty("Target Type", lvl -> Targettype.NONE castTo int)

@compiletime function gen()
    new AbilityDefinitionBerserk(ANIMAL_FORCE_NL)
        ..setLevels(1)
        ..presetButtonPosNormal(2, 2)
        ..setHeroAbility(false)
        ..general()
        // Stats
        ..addTooltipProperty("Extra damage", lvl -> EXTRA_DMG_NL)
        ..tooltipStopListen()
    
    new AbilityDefinitionBerserk(ANIMAL_FORCE_L)
        ..setLevels(2)
        ..presetButtonPosNormal(2, 2)
        ..presetButtonPosResearch(2, 0)
        ..setHeroAbility(true)
        ..setRequiredLevel(6)
        ..setLevelSkipRequirement(6)
        ..general()
        // Stats
        ..addTooltipProperty("Extra damage", lvl -> EXTRA_DMG_L[lvl - 1])
        ..tooltipStopListen()

    new AbilityDefinitionBerserk(ANIMAL_FORCE_S)
        ..setLevels(3)
        ..setHeroAbility(false)
        ..general()
        ..presetButtonPosNormal(2, 1)
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Animal Force")
        ..presetHotkey("F")
        // Stats
        ..presetManaCost(lvl -> MANA)
        ..presetTooltipNormalExtended(lvl -> begin
            real value
            if lvl < 2
                value = EXTRA_DMG_NL
            else
                value = EXTRA_DMG_L[lvl - 2]
            return "Increase the speed of the salamnder in " + (SPEED * 100).toString(0) + "% and he can go through to the units for 8 seconds and every spell he cast deal " + value.toString() + "extra damage."
        end)
        ..setEditorSuffix("(Stealed)")
        ..setName("Ult Animal Force")
    
    new BuffDefinition(ANIMAL_FORCE_BUFF, 'Bspe')
        ..setIcon(Icons.bTNStampede)
        ..setArtTarget(1, Abilities.volcanoDeath)
        ..setName(1, "Animal Force")
        ..setTooltipNormal(1, "Animal Force")
        .setTooltipNormalExtended(1, "This animal liberate his hidden powers.")