package Charge

import AbilityData
import MoveSpeedX

import FogModifier

public constant CHARGE_NL = compiletime(ABIL_ID_GEN.next())

// General
let CD = 10.
let RANGE = 1000.
let MANA = 70
let DMG = 50.
let SPEED = 600.
// Not leveling
// Internal
let targetMap = new HashMap<unit, unit>()
let runMap = new HashMap<unit, boolean>()
let casters = CreateGroup()
let INTERVAL = 0.05

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        if GetSpellAbilityId() == CHARGE_NL
            let caster = GetSpellAbilityUnit()
            let target = GetSpellTargetUnit()
            let eff = caster.addEffect(Imports.valiantChargeRoyal, "origin")
            let damage = DMG
            var vision = createFogModifier(caster.getOwner(), FOG_OF_WAR_VISIBLE, target.getPos(), 150., true, false)
            caster.issueTargetOrderById(Orders.smart, target)
            caster.setBaseSpeed("charge", SPEED)
            targetMap.put(caster, target)
            runMap.put(caster, true)
            casters.addUnit(caster)
            doPeriodically(INTERVAL, cb -> begin
                vision.destr()
                if runMap.get(caster)
                    vision = createFogModifier(caster.getOwner(), FOG_OF_WAR_VISIBLE, target.getPos(), 150., true, false)
                    if caster.getPos().distanceTo(target.getPos()) <= 150. or not target.isAlive()
                        if target.isAlive()
                            Damage.deal(caster, target, damage, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_ENHANCED, WEAPON_TYPE_METAL_HEAVY_BASH)
                        runMap.put(caster, false)
                else
                    caster.removeSpeedBuff("charge")
                    eff.destr()
                    casters.removeUnit(caster)
                    destroy cb
            end)
    end)
    code func = () -> begin
        let u = GetOrderedUnit()
        if casters.contains(u) and not GetOrderTarget() == targetMap.get(u)
            runMap.put(u, false)
    end
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, func)
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER, func)
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, func)

@compiletime function gen()
    new ChannelAbilityPreset(CHARGE_NL, 1, true, new AbilityTooltipGenerator("The Zealot concentrate in a target so increase his speed and causing a damage when reaches the target.|nThe spell ends if the Zealot do another action or is stopped."))
        ..presetBaseOrderID(lvl -> "chainlightning")
        ..presetOption(Option.PHYSICALSPELL, true)
        ..presetButtonPosNormal(0, 2)
        ..setHeroAbility(false)
        ..presetFollowThroughTime(lvl -> 0.75)
        ..setAnimationNames("spell,channel")
        ..presetTargetsAllowed(lvl -> "air,enemies,neutral,ground")
        ..tooltipStartListen()
        ..setName("[|cffffcc00Q|r] Charge")
        ..presetTargetTypes(Targettype.UNIT)
        // Stats
        ..addTooltipProperty("Damage", lvl -> DMG)
        ..addTooltipProperty("Move speed", lvl -> SPEED)
        ..presetManaCost(lvl -> MANA)
        ..presetCooldown(lvl -> CD)
        ..presetCastRange(lvl -> RANGE)
        // Audiovisual
        ..presetIcon(Imports.bTNCharge)
        // Tooltip
        ..presetHotkey("Q")
        .tooltipStopListen()