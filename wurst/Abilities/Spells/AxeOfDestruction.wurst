package AxeOfDestruction

import AbilityData

public constant AXE_OF_DESTRUCTION_NL = compiletime(ABIL_ID_GEN.next())
constant AXE_OF_DESTRUCTION_BUFF = compiletime(BUFF_ID_GEN.next())

// General
let CD = 90.
let RANGE = 150.
let AREA = 500.
// Not Leveling
let MANA_NL = 160
let DMG_NL = 300.
// Internal
let MAX_DURATION = 10.

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        if GetSpellAbilityId() == AXE_OF_DESTRUCTION_NL
            let caster = GetSpellAbilityUnit()
            let target = GetSpellTargetUnit()
            var speed = 1800.
            let damage = DMG_NL
            flashEffect(Abilities.thunderclapCaster, target.getPos())
            target.addAbility(AbilityIds.stormCrowForm)
            target.removeAbility(AbilityIds.stormCrowForm)
            doPeriodically(INTERVAL, cb -> begin
                speed -= 62.5
                if speed > -1800.
                    real rate
                    real goal
                    if speed >= 0.
                        rate = speed
                        goal = 800.
                    else
                        rate = -speed
                        goal = 0.
                    target.setFlyHeight(goal, rate)
                else
                    let pos = target.getPos()
                    let owner = caster.getOwner()
                    flashEffect(Abilities.thunderclapCaster, pos)
                    createRipple(2., false, pos, AREA, AREA, 64., 1., AREA)
                    forUnitsInRange(pos, AREA, u -> begin
                        if owner.isEnemyOf(u.getOwner()) and not u.isType(UNIT_TYPE_MAGIC_IMMUNE) and not u.isType(UNIT_TYPE_STRUCTURE)
                            Damage.deal(caster, u, damage, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_SONIC, WEAPON_TYPE_ROCK_HEAVY_BASH)
                            u.issuePointOrderById(Orders.move, u.getPos().moveTowards(pos, -AREA))
                    end)
                    target.setFlyHeight(target.getDefaultFlyHeight(), 1000.)
                    target.removeAbility(AXE_OF_DESTRUCTION_BUFF)
                    destroy cb
            end)
    end)

@compiletime function gen()
    new AbilityDefinitionThunderBoltCreep(AXE_OF_DESTRUCTION_NL)
        ..setLevels(1)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("The Axemaster gives a great attack on the ground where its target is, making it fly through the air and when it falls it causes a damage to the nearby units and makes them lose concentration."))
        ..presetButtonPosNormal(3, 2)
        ..setHeroAbility(false)
        ..setAnimationNames("slam")
        ..presetTargetsAllowed(lvl -> "enemies,debris,neutral,organic,ground")
        ..presetDurationHero(lvl -> MAX_DURATION)
        ..presetDurationNormal(lvl -> MAX_DURATION)
        ..presetBuffs(lvl -> int2fourchar(AXE_OF_DESTRUCTION_BUFF))
        ..tooltipStartListen()
        ..setName("[|cffffcc00R|r] Axe of Destruction")
        ..addTooltipProperty("Target Type", lvl -> Targettype.UNIT castTo int)
        // Stats
        ..addTooltipProperty("Damage", lvl -> DMG_NL)
        ..presetManaCost(lvl -> MANA_NL)
        ..presetCooldown(lvl -> CD)
        ..presetCastRange(lvl -> RANGE)
        ..presetAreaofEffect(lvl -> AREA)
        // Audiovisual
        ..presetIcon(Imports.bTNGromHellscreamsAxe)
        // Tooltip
        ..presetHotkey("F")
        .tooltipStopListen()
    
    new BuffDefinition(AXE_OF_DESTRUCTION_BUFF, 'BPSE')
        ..setIcon(Imports.bTNGromHellscreamsAxe)
        ..setName(1, "Axe of Destruction")
        ..setTooltipNormal(1, "Axe of Destruction")