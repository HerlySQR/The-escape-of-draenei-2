package AxeOfDestruction

import AbilityData

public constant AXE_OF_DESTRUCTION_NL = compiletime(ABIL_ID_GEN.next())
public constant AXE_OF_DESTRUCTION_L = compiletime(ABIL_ID_GEN.next())
public constant AXE_OF_DESTRUCTION_S = compiletime(ABIL_ID_GEN.next())

constant AXE_OF_DESTRUCTION_BUFF = compiletime(BUFF_ID_GEN.next())

// General
let CD = 90.
let RANGE = 150.
let AREA = 500.
// Not Leveling
let MANA_NL = 160
let DMG_NL = 300.
// Leveling
let MANA_L = [150, 250]
let DMG_L = [250., 500.]
// Internal
let MAX_DURATION = 10.

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        let data = abilityCond(AXE_OF_DESTRUCTION_NL, AXE_OF_DESTRUCTION_L, AXE_OF_DESTRUCTION_S)
        if data.check
            let caster = GetSpellAbilityUnit()
            let target = GetSpellTargetUnit()
            var speed = 1800.
            real damage
            if data.isNL
                damage = DMG_NL
            else
                damage = DMG_L[data.lvl]
            flashEffect(Abilities.thunderclapCaster, target.getPos())
            target.addAbility(AbilityIds.stormCrowForm)
            target.removeAbility(AbilityIds.stormCrowForm)
            doPeriodically(INTERVAL, cb -> begin
                speed -= 62.5
                if speed > -1800.
                    real rate
                    real goal
                    if speed >= 0.
                        rate = speed
                        goal = 800.
                    else
                        rate = -speed
                        goal = 0.
                    target.setFlyHeight(goal, rate)
                else
                    let pos = target.getPos()
                    let owner = caster.getOwner()
                    flashEffect(Abilities.thunderclapCaster, pos)
                    createRipple(2., false, pos, AREA, AREA, 64., 1., AREA)
                    forUnitsInRange(pos, AREA, u -> begin
                        if owner.isEnemyOf(u.getOwner()) and not u.isType(UNIT_TYPE_MAGIC_IMMUNE) and not u.isType(UNIT_TYPE_STRUCTURE)
                            caster.damageTarget(u, damage, true, false, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_SONIC, WEAPON_TYPE_ROCK_HEAVY_BASH)
                            u.issuePointOrderById(Orders.move, u.getPos().moveTowards(pos, -AREA))
                    end)
                    target.setFlyHeight(target.getDefaultFlyHeight(), 1000.)
                    target.removeAbility(AXE_OF_DESTRUCTION_BUFF)
                    destroy cb
            end)
    end)

function AbilityDefinitionThunderBoltCreep.general()
    this
        ..setHeroAbility(false)
        ..setAnimationNames("slam")
        ..presetTargetsAllowed(lvl -> "enemies,debris,neutral,organic,ground")
        ..presetDurationHero(lvl -> MAX_DURATION)
        ..presetDurationNormal(lvl -> MAX_DURATION)
        ..presetBuffs(lvl -> AXE_OF_DESTRUCTION_BUFF.toRawCode())
        ..tooltipStartListen()
        ..setName("Axe of Destruction")
        ..addTooltipProperty("Target Type", lvl -> Targettype.UNIT castTo int)
        ..presetCooldown(lvl -> CD)
        ..presetCastRange(lvl -> RANGE)
        ..presetAreaofEffect(lvl -> AREA)
        ..presetIcon(Imports.bTNGromHellscreamsAxe)

@compiletime function gen()
    new AbilityDefinitionThunderBoltCreep(AXE_OF_DESTRUCTION_NL)
        ..setLevels(1)
        ..presetButtonPosNormal(3, 2)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("The Axemaster gives a great attack on the ground where its target is, making it fly through the air and when it falls it causes a damage to the nearby units and makes them lose concentration."))
        ..general()
        // Stats
        ..addTooltipProperty("Damage", lvl -> DMG_NL)
        ..presetManaCost(lvl -> MANA_NL)
        // Audiovisual
        // Tooltip
        ..presetHotkey("R")
        .tooltipStopListen()
    
    new AbilityDefinitionThunderBoltCreep(AXE_OF_DESTRUCTION_L)
        ..setLevels(2)
        ..setRequiredLevel(6)
        ..setLevelSkipRequirement(6)
        ..presetButtonPosNormal(3, 2)
        ..presetButtonPosResearch(3, 0)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("The Axemaster gives a great attack on the ground where its target is, making it fly through the air and when it falls it causes a damage to the nearby units and makes them lose concentration."))
        ..general()
        // Stats
        ..addTooltipProperty("Damage", lvl -> DMG_L[lvl - 1])
        ..presetManaCost(lvl -> MANA_L[lvl - 1])
        // Audiovisual
        // Tooltip
        ..presetHotkey("R")
        .tooltipStopListen()

    new AbilityDefinitionThunderBoltCreep(AXE_OF_DESTRUCTION_S)
        ..setLevels(3)
        ..setHeroAbility(false)
        ..general()
        ..presetButtonPosNormal(2, 1)
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Axe of Destruction")
        ..presetHotkey("F")
        // Stats
        ..presetManaCost(lvl -> lvl < 2 ? MANA_NL : MANA_L[lvl - 2])
        ..presetTooltipNormalExtended(lvl -> begin
            real value
            if lvl < 2
                value = DMG_NL
            else
                value = DMG_L[lvl - 2]
            return "The Axemaster gives a great attack on the ground where its target is, making it fly through the air and when it falls it causes {0} of damage to the nearby units and makes them lose concentration.".format(value.toString())
        end)
        ..setEditorSuffix("(Stealed)")
        ..setName("Ult Axe of Destruction")
    
    new BuffDefinition(AXE_OF_DESTRUCTION_BUFF, 'BPSE')
        ..setIcon(Imports.bTNGromHellscreamsAxe)
        ..setName(1, "Axe of Destruction")
        ..setTooltipNormal(1, "Axe of Destruction")