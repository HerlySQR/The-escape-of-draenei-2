package InstillFear

import AbilityData

public constant INSTILL_FEAR_NL = compiletime(ABIL_ID_GEN.next())
public constant INSTILL_FEAR_L = compiletime(ABIL_ID_GEN.next())
public constant INSTILL_FEAR_S = compiletime(ABIL_ID_GEN.next())

// General
let DURATION = 4.
let CD = 80.
let AREA = 900.
// Not leveling
let DMG_NL = 140.
let MANA_NL = 150
// Leveling
let DMG_L = [175., 300.]
let MANA_L = [175, 250]

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        if abilityCond(INSTILL_FEAR_NL, INSTILL_FEAR_L, INSTILL_FEAR_S).check
            let caster = GetSpellAbilityUnit()
            caster.flashEffect(Abilities.roarCaster, "chest")
            caster.flashEffect(Abilities.tauntCaster, "chest")
            caster.flashEffect(Imports.fireNova2, "chest")
    end)

function AbilityDefinitionTaurenChieftainWarStomp.general()
    this
        ..setArtCaster(Abilities.howlCaster)
        ..presetTargetsAllowed(lvl -> "air,organic,ground")
        ..tooltipStartListen()
        ..setHotkey("R")
        ..setName("Instill Fear")
        ..addTooltipProperty("Target Type", lvl -> Targettype.NONE castTo int)
        // Stats
        ..presetCooldown(lvl -> CD)
        ..presetAreaofEffect(lvl -> AREA)
        ..presetDurationHero(lvl -> DURATION)
        ..presetDurationNormal(lvl -> DURATION)
        // Audiovisual
        ..presetIcon(Icons.bTNBerserk)

@compiletime function gen()
    new AbilityDefinitionTaurenChieftainWarStomp(INSTILL_FEAR_NL)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("Roars loudly that stuns the enemy units that are still in range of the Cacique and causes damage."))
        ..presetLevels(1)
        ..setHeroAbility(false)
        ..general()
        ..presetDamage(lvl -> DMG_NL)
        ..presetManaCost(lvl -> MANA_NL)
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Not leveling)")
    
    new AbilityDefinitionTaurenChieftainWarStomp(INSTILL_FEAR_L)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("Roars loudly that stuns the enemy units that are still in range of the Cacique and causes damage."))
        ..presetLevels(2)
        ..setHeroAbility(true)
        ..setRequiredLevel(6)
        ..setLevelSkipRequirement(6)
        ..general()
        ..presetDamage(lvl -> DMG_L[lvl-1])
        ..presetManaCost(lvl -> MANA_L[lvl-1])
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Leveling)")
    
    new AbilityDefinitionTaurenChieftainWarStomp(INSTILL_FEAR_S)
        ..setHeroAbility(false)
        ..general()
        ..presetLevels(3)
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Instill Fear")
        ..setHotkey("F")
        // Stats
        ..presetDamage(lvl -> lvl < 2 ? DMG_NL : DMG_L[lvl-2])
        ..presetManaCost(lvl -> lvl < 2 ? MANA_NL : MANA_L[lvl-2])
        ..presetTooltipNormalExtended(lvl -> begin
            return "Roars loudly that stuns the enemy units that are still {0} in range of he for {1} seconds and causing {2} damage.".format(AREA.toString(), DURATION.toString(), (lvl < 2 ? DMG_NL : DMG_L[lvl-2]).toString())
        end)
        ..setEditorSuffix("(Stealed)")
        ..setName("Ult Instill Fear")