package Track

import AbilityData

public constant TRACK_NL = compiletime(ABIL_ID_GEN.next())
public constant TRACK_L = compiletime(ABIL_ID_GEN.next())
public constant TRACK_S = compiletime(ABIL_ID_GEN.next())

constant TRACK_BUFF = compiletime(BUFF_ID_GEN.next())

// General
let CD = 6.
let RANGE = 1000.
let DURATION = 25.
let TARGETS = "air,ground,enemy,neutral"
// Not leveling
let MANA_NL = 25
let ARMOR_DEC_NL = 10
// Leveling
let MANA_L = [30, 35]
let ARMOR_DEC_L = [11, 17]

function AbilityDefinitionFaerieFire.general()
    this
        ..presetBuffs(lvl -> TRACK_BUFF.toRawCode())
        ..presetTargetsAllowed(lvl -> TARGETS)
        ..setRequirements("")
        ..tooltipStartListen()
        ..setName("Track")
        ..addTooltipProperty("Target Type", lvl -> Targettype.UNIT castTo int)
        // Stats
        ..presetCooldown(lvl -> CD)
        ..presetCastRange(lvl -> RANGE)
        ..presetDurationHero(lvl -> DURATION)
        ..presetDurationNormal(lvl -> DURATION)
        ..presetTooltipTurnOff(lvl -> "|cffc3dbffClick the right mouse button to activate the automatic action.|r")
        ..presetTooltipTurnOffExtended(lvl -> "|cffc3dbffClick the right mouse button to deactivate the automatic action.|r")

@compiletime function gen()
    new AbilityDefinitionFaerieFire(TRACK_NL)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("Reduces the armor of selected enemy unit and makes them visible.\nLasts " + DURATION.toString(0) + " seconds."))
        ..presetLevels(1)
        ..presetButtonPosNormal(3, 2)
        ..setHeroAbility(false)
        ..general()
        ..presetDefenseReduction(lvl -> ARMOR_DEC_NL)
        ..presetManaCost(lvl -> MANA_NL)
        // Audiovisual
        // Tooltip
        ..presetHotkey("R")
        ..tooltipStopListen()
    
    new AbilityDefinitionFaerieFire(TRACK_L)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("Reduces the armor of selected enemy unit and makes them visible.\nLasts " + DURATION.toString(0) + " seconds."))
        ..presetLevels(2)
        ..presetButtonPosNormal(3, 2)
        ..presetButtonPosResearch(3, 0)
        ..setHeroAbility(true)
        ..general()
        ..presetDefenseReduction(lvl -> ARMOR_DEC_L[lvl-1])
        ..presetManaCost(lvl -> MANA_L[lvl-1])
        // Audiovisual
        // Tooltip
        ..presetHotkey("R")
        ..tooltipStopListen()
    
    new AbilityDefinitionFaerieFire(TRACK_S)
        ..presetLevels(3)
        ..setHeroAbility(false)
        ..general()
        ..presetButtonPosNormal(2, 1)
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Track")
        ..presetHotkey("F")
        // Stats
        ..presetDefenseReduction(lvl -> lvl < 2 ? ARMOR_DEC_NL : ARMOR_DEC_L[lvl-2])
        ..presetManaCost(lvl -> lvl < 2 ? MANA_NL : MANA_L[lvl-2])
        ..presetTooltipNormalExtended(lvl -> "Reduces the armor of selected enemy unit by {0} and makes them visible.\nLasts {1} seconds.".format((lvl < 2 ? ARMOR_DEC_NL : ARMOR_DEC_L[lvl-2]).toString(), DURATION.toString(0)))
        ..setEditorSuffix("(Stealed)")
        .setName("Ult Track")
    
    new BuffDefinition(TRACK_BUFF, 'Bfae')
        ..setName(1, "Track")
        ..setTooltipNormal(1, "Track")
        .setTooltipNormalExtended(1, "This unit is Tracked: its armor has been reduced and can be detected by the enemy.")