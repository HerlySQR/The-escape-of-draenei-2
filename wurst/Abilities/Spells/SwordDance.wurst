package SwordDance

import AbilityData

public constant SWORD_DANCE_NL = compiletime(ABIL_ID_GEN.next())

// General
let CD = 15.
let AREA = 350.
// Not leveling
let MANA_NL = 100
let DMG_NL = 100.
// Internal
let effMap = new HashMap<unit, effect>()
let destrMap = new HashMap<unit, boolean>()

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST, () -> begin
        if GetSpellAbilityId() == SWORD_DANCE_NL
            let caster = GetSpellAbilityUnit()
            if effMap.has(caster)
                effMap.get(caster).destr()
            effMap.put(caster, caster.addEffect(Imports.arcaneTornado, "origin"))
            destrMap.put(caster, true)
    end)
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_ENDCAST, () -> begin
        if GetSpellAbilityId() == SWORD_DANCE_NL
            let caster = GetSpellAbilityUnit()
            if destrMap.get(caster)
                effMap.get(caster).destr()
            effMap.remove(caster)
            destrMap.remove(caster)
    end)
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        if GetSpellAbilityId() == SWORD_DANCE_NL
            let caster = GetSpellAbilityUnit()
            let owner = caster.getOwner()
            let damage = DMG_NL
            destrMap.put(caster, false)
            forUnitsInRange(caster.getPos(), AREA, u -> begin
                if owner.isEnemyOf(u.getOwner()) and not u.isType(UNIT_TYPE_STRUCTURE)
                    u.flashEffect(Abilities.stampedeMissileDeath, "chest")
                    Damage.deal(caster, u, damage, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_ENHANCED)
            end)
            let eff = effMap.get(caster)
            doAfter(0.27, () -> eff.destr())
    end)

@compiletime function gen()
    new ChannelAbilityPreset(SWORD_DANCE_NL, 1, true, new AbilityTooltipGenerator("The zealot with its blade, with a single slash deal damage to all nearby enemy units."))
        ..presetBaseOrderID(lvl -> "earthquake")
        ..presetButtonPosNormal(1, 2)
        ..setHeroAbility(false)
        ..presetFollowThroughTime(lvl -> 0.75)
        ..setAnimationNames("attack")
        ..tooltipStartListen()
        ..setName("[|cffffcc00W|r] Sword dance")
        ..presetTargetTypes(Targettype.NONE)
        // Stats
        ..addTooltipProperty("Damage", lvl -> DMG_NL)
        ..presetAreaofEffect(lvl -> AREA)
        ..presetManaCost(lvl -> MANA_NL)
        ..presetCooldown(lvl -> CD)
        // Audiovisual
        ..presetIcon(Icons.bTNWhirlwind)
        // Tooltip
        ..presetHotkey("W")
        .tooltipStopListen()