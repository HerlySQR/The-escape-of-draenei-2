package FeralRampage

import AbilityData
import PseudoWindWalk

public constant FERAL_RAMPAGE_NL = compiletime(ABIL_ID_GEN.next())
public constant FERAL_RAMPAGE_L = compiletime(ABIL_ID_GEN.next())
public constant FERAL_RAMPAGE_S = compiletime(ABIL_ID_GEN.next())

constant FERAL_RAMPAGE_BUFF = compiletime(BUFF_ID_GEN.next())

// General
let SPEED = 1.75
let DURATION = 8.
let CD = 45.
let MANA = 100
// Not Leveling
let EXTRA_DMG_NL = 100.
// Leveling
let EXTRA_DMG_L = [150., 225.]

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        let data = abilityCond(FERAL_RAMPAGE_NL, FERAL_RAMPAGE_L, FERAL_RAMPAGE_S)
        if data.check
            GetSpellAbilityUnit().startWindWalk(FERAL_RAMPAGE_BUFF)
    end)
    DamageEvent.addUnreducedListener(() -> begin
        let source = DamageEvent.getSource()
        if source.hasAbility(FERAL_RAMPAGE_BUFF) and DamageEvent.isSpell()
            if whatMode[5] == NOT_LEVELING
                let level = max(source.getAbilityLevel(FERAL_RAMPAGE_NL), source.getAbilityLevel(FERAL_RAMPAGE_S))
                if level > 0
                    DamageEvent.addAmount(EXTRA_DMG_NL)
            else if whatMode[5] == LEVELING
                let level = max(source.getAbilityLevel(FERAL_RAMPAGE_L), source.getAbilityLevel(FERAL_RAMPAGE_S) - 1)
                if level > 0
                    DamageEvent.addAmount(EXTRA_DMG_L[level - 1])
    end)

function AbilityDefinitionBerserk.general()
    this
        ..presetBuffs(lvl -> FERAL_RAMPAGE_BUFF.toRawCode())
        ..setEffectSound("StampedeCast")
        ..presetDurationHero(lvl -> DURATION)
        ..presetDurationNormal(lvl -> DURATION)
        ..presetDamageTakenIncrease(lvl -> 0.)
        ..presetAttackSpeedIncrease(lvl -> 0.)
        ..presetMovementSpeedIncrease(lvl -> SPEED)
        ..tooltipStartListen()
        ..setHotkey("R")
        ..setName("Feral Rampage")
        ..addTooltipProperty("Duration", lvl -> DURATION)
        ..presetCooldown(lvl -> CD)
        ..presetManaCost(lvl -> MANA)
        // Audiovisual
        ..presetIcon(Icons.bTNStampede)
        // Tooltip
        ..addTooltipProperty("Target Type", lvl -> Targettype.NONE castTo int)

@compiletime function gen()
    new AbilityDefinitionBerserk(FERAL_RAMPAGE_NL)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("Increase the speed of the salamnder in " + (SPEED * 100).toString(0) + "% and he can go through to the units for 8 seconds and every spell he cast deal extra damage."))
        ..presetLevels(1)
        ..setHeroAbility(false)
        ..general()
        // Stats
        ..addTooltipProperty("Extra damage", lvl -> EXTRA_DMG_NL)
        ..tooltipStopListen()
        .setEditorSuffix("(Not leveling)")
    
    new AbilityDefinitionBerserk(FERAL_RAMPAGE_L)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("Increase the speed of the salamnder in " + (SPEED * 100).toString(0) + "% and he can go through to the units for 8 seconds and every spell he cast deal extra damage."))
        ..presetLevels(2)
        ..setHeroAbility(true)
        ..setRequiredLevel(6)
        ..setLevelSkipRequirement(6)
        ..general()
        // Stats
        ..addTooltipProperty("Extra damage", lvl -> EXTRA_DMG_L[lvl - 1])
        ..tooltipStopListen()
        .setEditorSuffix("(Leveling)")

    new AbilityDefinitionBerserk(FERAL_RAMPAGE_S)
        ..presetLevels(3)
        ..setHeroAbility(false)
        ..general()
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Feral Rampage")
        ..setHotkey("F")
        // Stats
        ..presetManaCost(lvl -> MANA)
        ..presetTooltipNormalExtended(lvl -> begin
            real value
            if lvl < 2
                value = EXTRA_DMG_NL
            else
                value = EXTRA_DMG_L[lvl - 2]
            return "Increase the speed of the salamnder in " + (SPEED * 100).toString(0) + "% and he can go through to the units for 8 seconds and every spell he cast deal " + value.toString() + "extra damage."
        end)
        ..setEditorSuffix("(Stealed)")
        ..setName("Ult Feral Rampage")
    
    new BuffDefinition(FERAL_RAMPAGE_BUFF, 'Bspe')
        ..setIcon(Icons.bTNStampede)
        ..setArtTarget(1, Abilities.volcanoDeath)
        ..setName(1, "Feral Rampage")
        ..setTooltipNormal(1, "Feral Rampage")
        .setTooltipNormalExtended(1, "This animal liberate his hidden powers.")