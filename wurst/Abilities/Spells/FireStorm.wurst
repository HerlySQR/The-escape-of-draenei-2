package FireStorm

import AbilityData

public constant FIRE_STORM_NL = compiletime(ABIL_ID_GEN.next())
public constant FIRE_STORM_L = compiletime(ABIL_ID_GEN.next())
public constant FIRE_STORM_S = compiletime(ABIL_ID_GEN.next())

// General
let DURATION = 20.
let CD = 120.
let RANGE = 300.
// Not leveling
let DMG_NL = 60.
let MANA_NL = 150
// Not leveling
let DMG_L = [65., 110.]
let MANA_L = [150, 200]
// Internal
let AREA = 1000.
let COLLISION = 48.
let AREA_DMG = 150.
let INTERVAL = 0.1
let SPEED = 500.
let MAX_DIST = compiletime(2*AREA)
let MODEL = Imports.fireballMinor

function check(Missiles m, unit u) returns boolean
    return m.owner.isEnemyOf(u.getOwner()) and u.isAlive() and not u.isType(UNIT_TYPE_MAGIC_IMMUNE)

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        let data = abilityCond(FIRE_STORM_NL, FIRE_STORM_L, FIRE_STORM_S)
        if data.check
            let caster = EventData.getSpellAbilityUnit()
            let owner = caster.getOwner()
            let center = caster.getPos()
            let face = center.angleTo(EventData.getSpellTargetPos())
            let offset = center.polarOffset(face, -AREA)
            let vector1 = (face - angle(PIHALF)).toVec(1.)
            let vector2 = face.toVec(MAX_DIST)

            real damage
            if data.isNL
                damage = DMG_NL
            else
                damage = DMG_L[data.lvl]

            doPeriodically(INTERVAL, cb -> begin
                if caster.getCurrentOrder() == Orders.stampede
                    let delta = GetRandomReal(-AREA, AREA)
                    let fromPos = offset + vector1 * delta
                    let toPos = fromPos + vector2
                    let m = new Missiles(fromPos.withZ(50.), toPos.withZ(50.))
                    m.source = caster
                    m.owner = owner
                    m.setModel(MODEL)
                    m.setScale(0.25)
                    m.setSpeed(SPEED)
                    m.collision = COLLISION
                    m.damage = damage
                    m.onHit(hit -> begin
                        let b = check(m, hit)
                        if b
                            forUnitsInRange(m.getPos().toVec2(), AREA_DMG, u -> begin
                                if check(m, u)
                                    m.source.damageTarget(u, m.damage, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_FIRE)
                            end)
                        return b
                    end)
                    m.launch()
                else
                    destroy cb
            end)
    end)

function ChannelAbilityPreset.general()
    this
        ..presetBaseOrderID(lvl -> "stampede")
        ..tooltipStartListen()
        ..setHotkey("R")
        ..setName("Fire storm")
        ..presetTargetTypes(Targettype.POINT)
        // Stats
        ..presetFollowThroughTime(lvl -> DURATION)
        ..presetCastRange(lvl -> RANGE)
        ..presetCooldown(lvl -> CD)
        // Audiovisual
        ..presetIcon(Imports.bTNRainOfFire)
        // Tooltip
        ..setAnimationNames("morph,looping")

@compiletime function gen()
    new ChannelAbilityPreset(FIRE_STORM_NL, 1, true, new AbilityTooltipGenerator("Summons a firestorm to explode on enemies.\nLasts " + DURATION.toString(0) + " seconds."))
        ..setHeroAbility(false)
        ..general()
        ..addTooltipProperty("Damage", lvl -> DMG_NL)
        ..presetManaCost(lvl -> MANA_NL)
        ..tooltipStopListen()
        ..setEditorSuffix("(Not leveling)")
    
    new ChannelAbilityPreset(FIRE_STORM_L, 2, true, new AbilityTooltipGenerator("Summons a firestorm to explode on enemies.\nLasts " + DURATION.toString(0) + " seconds."))
        ..setRequiredLevel(6)
        ..setLevelSkipRequirement(6)
        ..setHeroAbility(true)
        ..general()
        ..addTooltipProperty("Damage", lvl -> DMG_L[lvl-1])
        ..presetManaCost(lvl -> MANA_L[lvl-1])
        ..tooltipStopListen()
        ..setEditorSuffix("(Leveling)")

    new ChannelAbilityPreset(FIRE_STORM_S, 3, true)
        ..presetLevels(3)
        ..setHeroAbility(false)
        ..general()
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Fire storm")
        ..setHotkey("F")
        // Stats
        ..presetManaCost(lvl -> lvl < 2 ? MANA_NL : MANA_L[lvl-2])
        ..presetTooltipNormalExtended(lvl -> "Summons a firestorm to explode on enemies causing {0} damage.\nLasts {1} seconds.".format((lvl < 2 ? DMG_NL : DMG_L[lvl-2]).toString(0), DURATION.toString(0)))
        ..setEditorSuffix("(Stealed)")
        ..setName("Ult Fire storm")