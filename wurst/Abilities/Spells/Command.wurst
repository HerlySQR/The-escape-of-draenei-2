package Command

import AbilityData

public constant COMMAND_NL = compiletime(ABIL_ID_GEN.next())
public constant COMMAND_L = compiletime(ABIL_ID_GEN.next())

constant COMMAND_AURA = compiletime(ABIL_ID_GEN.next())
constant COMMAND_BUFF = compiletime(BUFF_ID_GEN.next())

// General
let AREA = 700.
let DMG_FACTOR_CHARGE = 0.05
let CHARGE_DURATION = 10
// Not leveling
let MAX_CHARGES_NL = [6, 12]
// Leveling
let MAX_CHARGES_L = [4, 8, 12, 16]
// Internal
let chargeMap = new HashMap<unit, int>()

init
    DamageEvent.addListener(() -> begin
        let data = DamageEvent.getSource().pasAbilityCond(COMMAND_NL, COMMAND_L)
        if data.check
            let master = DamageEvent.getSource()
            int maxCharges
            if data.isNL
                maxCharges = MAX_CHARGES_NL[data.lvl]
            else
                maxCharges = MAX_CHARGES_L[data.lvl]
            chargeMap.put(master, CHARGE_DURATION)
            if not master.hasAbility(COMMAND_AURA)
                master.addAbility(COMMAND_AURA)
                doPeriodically(1., cb -> begin
                    if master.isAlive()
                        let count = chargeMap.get(master) - 1
                        if count > 0
                            chargeMap.put(master, count)
                        else
                            if master.getAbilityLevel(COMMAND_AURA) > 1
                                master.setAbilityLevel(COMMAND_AURA, master.getAbilityLevel(COMMAND_AURA) - 1)
                            else
                                master.removeAbility(COMMAND_AURA)
                                chargeMap.remove(master)
                                destroy cb
                    else
                        master.removeAbility(COMMAND_AURA)
                        chargeMap.remove(master)
                        destroy cb
                end)
            else if master.getAbilityLevel(COMMAND_AURA) < maxCharges
                master.setAbilityLevel(COMMAND_AURA, master.getAbilityLevel(COMMAND_AURA) + 1)
    end)

function AbilityDefinitionHardenedSkin.general()
    this
        ..presetIgnoredDamage(lvl -> 0.)
        ..presetMinimumDamage(lvl -> 0.)
        ..presetChancetoReduceDamage(lvl -> 0.)
        ..presetIncludeMeleeDamage(lvl -> false)
        ..presetIncludeRangedDamage(lvl -> false)
        ..setRequirements("")
        ..tooltipStartListen()
        ..setName("Command")
        ..addTooltipProperty("Target Type", lvl -> Targettype.PASSIVE castTo int)
        ..presetIcon(Imports.pASHellscreamBerserk)

@compiletime function gen()
    new AbilityDefinitionHardenedSkin(COMMAND_NL)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("The command of the Axemaster grows when he evidence his power, each time he attacks the Axemaster creates an aura of damage that increases by " + (DMG_FACTOR_CHARGE * 100.).toString(0) + "% with each attack.\nThe charges loses after " + CHARGE_DURATION.toString() + " seconds."))
        ..presetLevels(2)
        ..presetButtonPosNormal(2, 2)
        ..setHeroAbility(false)
        ..general()
        // Stats
        ..addTooltipProperty("Max charges", lvl -> MAX_CHARGES_NL[lvl-1])
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Not leveling)")
    
    new AbilityDefinitionHardenedSkin(COMMAND_L)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("The command of the Axemaster grows when he evidence his power, each time he attacks the Axemaster creates an aura of damage that increases by " + (DMG_FACTOR_CHARGE * 100.).toString(0) + "% with each attack.\nThe charges loses after " + CHARGE_DURATION.toString() + " seconds."))
        ..presetLevels(4)
        ..setRequiredLevel(1)
        ..presetButtonPosNormal(2, 2)
        ..presetButtonPosResearch(2, 0)
        ..setHeroAbility(true)
        ..setIconResearch(Imports.bTNHellscreamBerserk)
        ..general()
        // Stats
        ..addTooltipProperty("Max charges", lvl -> MAX_CHARGES_L[lvl-1])
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        .setEditorSuffix("(Leveling)")
    
    new AbilityDefinitionAuraCommandCreep(COMMAND_AURA)
        ..presetLevels(16)
        ..presetAreaofEffect(lvl -> AREA)
        ..presetAttackDamageIncrease(lvl -> DMG_FACTOR_CHARGE * lvl)
        ..presetMeleeBonus(lvl -> true)
        ..presetRangedBonus(lvl -> true)
        ..presetTargetsAllowed(lvl -> "air,ground,friend,self,vuln,invu")
        ..presetBuffs(lvl -> COMMAND_BUFF.toRawCode())
        ..setItemAbility(true)
        ..presetButtonPosNormal(0, -11)
        ..presetIcon(Imports.pASHellscreamBerserk)
        ..setName("Command")
        ..setEditorSuffix("(Aura)")
    
    new BuffDefinition(COMMAND_BUFF, 'BOac')
        ..setIcon(Imports.pASHellscreamBerserk)
        ..setName(1, "Command")
        ..setTooltipNormal(1, "Command")
        .setTooltipNormalExtended(1, "This unit is under the effects of Command: it has an increased attack damage.")