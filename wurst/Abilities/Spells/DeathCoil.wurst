package DeathCoil

import AbilityData

public constant DEATH_COIL_NL = compiletime(ABIL_ID_GEN.next())
public constant DEATH_COIL_L = compiletime(ABIL_ID_GEN.next())
public constant DEATH_COIL_S = compiletime(ABIL_ID_GEN.next())

// General
let CD = 5.
let RANGE = 500.
let TARGETS = "air,invulnerable,organic,ground,vulnerable"
// Not leveling
let MANA_NL = [75, 90]
let HEAL_NL = [200., 250.]
// Leveling
let MANA_L = [75, 85, 95, 105]
let HEAL_L = [200., 250., 300., 350.]

function AbilityDefinitionDeathKnightDeathCoil.general()
    this
        ..presetTargetsAllowed(lvl -> TARGETS)
        ..setRequirements("")
        ..tooltipStartListen()
        ..setHotkey("Q")
        ..setName("Death Coil")
        ..addTooltipProperty("Target Type", lvl -> Targettype.UNIT castTo int)
        ..presetCastRange(lvl -> RANGE)
        ..presetCooldown(lvl -> CD)

@compiletime function gen()
    new AbilityDefinitionDeathKnightDeathCoil(DEATH_COIL_NL)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("A death coil that can heal impact points from a friendly unit of undead, or cause half damage to a friendly unit of living."))
        ..presetLevels(2)
        ..setHeroAbility(false)
        ..general()
        // Stats
        ..presetAmountHealedDamaged(lvl -> HEAL_NL[lvl-1])
        ..presetManaCost(lvl -> MANA_NL[lvl-1])
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Not leveling)")
    
    new AbilityDefinitionDeathKnightDeathCoil(DEATH_COIL_L)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("A death coil that can heal impact points from a friendly unit of undead, or cause half damage to a friendly unit of living."))
        ..presetLevels(4)
        ..setHeroAbility(true)
        ..general()
        // Stats
        ..presetAmountHealedDamaged(lvl -> HEAL_L[lvl-1])
        ..presetManaCost(lvl -> MANA_L[lvl-1])
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Leveling)")

    new AbilityDefinitionDeathKnightDeathCoil(DEATH_COIL_S)
        ..presetLevels(6)
        ..setHeroAbility(false)
        ..general()
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Death Coil")
        ..setHotkey("F")
        // Stats
        ..presetAmountHealedDamaged(lvl -> lvl < 3 ? HEAL_NL[lvl-1] : HEAL_L[lvl-3])
        ..presetManaCost(lvl -> lvl < 3 ? MANA_NL[lvl-1] : MANA_L[lvl-3])
        ..presetTooltipNormalExtended(lvl -> begin
            real value
            if lvl < 3
                value = HEAL_NL[lvl-1]
            else
                value = HEAL_L[lvl-3]
            return "A death coil that can heal {0} impact points from a friendly unit of undead, or cause half damage to a friendly unit of living.".format(value.toString(0))
        end)
        ..setEditorSuffix("(Stealed)")