package HowlOfTerror

import AbilityData

public constant HOWL_OF_TERROR_NL = compiletime(ABIL_ID_GEN.next())
public constant HOWL_OF_TERROR_L = compiletime(ABIL_ID_GEN.next())
public constant HOWL_OF_TERROR_S = compiletime(ABIL_ID_GEN.next())

// General
let CD = 20.
let DURATION = 15.
let AREA = 800.
let TARGETS = "air,ground,enemy,neutral"
// Not leveling
let ARMOR_DEC_NL = [4, 6]
let DMG_DEC_NL = [0.35, 0.45]
let MANA_NL = 100
// Leveling
let ARMOR_DEC_L = [3, 4, 5, 6]
let DMG_DEC_L = [0.2, 0.3, 0.4, 0.5]
let MANA_L =[90, 105, 120, 135]

function AbilityDefinitionPitLordHowlofTerror.general()
    this
        ..presetBuffs(lvl -> "BNht")
        ..presetTargetsAllowed(lvl -> TARGETS)
        ..tooltipStartListen()
        ..setHotkey("W")
        ..setName("Howl of terror")
        ..addTooltipProperty("Target Type", lvl -> Targettype.NONE castTo int)
        // Stats
        ..presetCooldown(lvl -> CD)
        ..presetAreaofEffect(lvl -> AREA)
        ..presetDurationHero(lvl -> DURATION)
        ..presetDurationNormal(lvl -> DURATION)

@compiletime function gen()
    new AbilityDefinitionPitLordHowlofTerror(HOWL_OF_TERROR_NL)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("It emits a terrifying howl that reduces the attack damage of nearby enemy units and armor for " + DURATION.toString(0) + " seconds."))
        ..presetLevels(2)
        ..setHeroAbility(false)
        ..presetDefenseIncrease(lvl -> ARMOR_DEC_NL[lvl-1])
        ..presetDamageIncrease(lvl -> DMG_DEC_NL[lvl-1])
        ..general()
        ..addTooltipProperty("Defense decrease", lvl -> ARMOR_DEC_NL[lvl-1])
        ..addTooltipProperty("Damage decrease", lvl -> DMG_DEC_NL[lvl-1])
        ..presetManaCost(lvl -> MANA_NL)
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Not leveling)")
    
    new AbilityDefinitionPitLordHowlofTerror(HOWL_OF_TERROR_L)
        ..registerTooltipGenerator(new AbilityTooltipGenerator("It emits a terrifying howl that reduces the attack damage of nearby enemy units and armor for " + DURATION.toString(0) + " seconds."))
        ..presetLevels(4)
        ..setHeroAbility(true)
        ..setRequiredLevel(1)
        ..presetDefenseIncrease(lvl -> ARMOR_DEC_L[lvl-1])
        ..presetDamageIncrease(lvl -> DMG_DEC_L[lvl-1])
        ..general()
        ..addTooltipProperty("Defense decrease", lvl -> ARMOR_DEC_L[lvl-1])
        ..addTooltipProperty("Damage decrease", lvl -> DMG_DEC_L[lvl-1])
        ..presetManaCost(lvl -> MANA_L[lvl-1])
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Leveling)")
    
    new AbilityDefinitionPitLordHowlofTerror(HOWL_OF_TERROR_S)
        ..setLevels(6)
        ..setHeroAbility(false)
        ..general()
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Howl of terror")
        ..setHotkey("F")
        // Stats
        ..presetDefenseIncrease(lvl -> lvl < 3 ? ARMOR_DEC_NL[lvl-1] : ARMOR_DEC_L[lvl-3])
        ..presetDamageIncrease(lvl -> lvl < 3 ? DMG_DEC_NL[lvl-1] : DMG_DEC_L[lvl-3])
        ..presetTooltipNormalExtended(lvl -> "It emits a terrifying howl that reduces {0}% of the attack damage of nearby enemy units and {1} of armor for {2} seconds.".format(((lvl < 3 ? DMG_DEC_NL[lvl-1] : DMG_DEC_L[lvl-3])*100).toInt().toString(), (lvl < 3 ? ARMOR_DEC_NL[lvl-1] : ARMOR_DEC_L[lvl-3]).toString(), DURATION.toString(0)))
        ..setEditorSuffix("(Stealed)")