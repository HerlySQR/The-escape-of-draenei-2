package DarkSummoning

import AbilityData

import initlater HeroSelection
import PlayerData

public constant DARK_SUMMONING_NL = compiletime(ABIL_ID_GEN.next())
public constant DARK_SUMMONING_L = compiletime(ABIL_ID_GEN.next())
public constant DARK_SUMMONING_S = compiletime(ABIL_ID_GEN.next())

// General
let AREA = 700.
let MANA = 100
// Not leveling
let DELAY_NL = [5., 3.]
let CD_NL = [100., 80.]
// Not leveling
let DELAY_L = [5., 4., 3., 2.]
let CD_L = [100., 80., 70., 70.]
// Internal
let SOUND_1 = new SoundDefinition(Sounds.possessionMissileLaunch1, false, true)
let SOUND_2 = new SoundDefinition(Sounds.possessionMissileHit1, false, true)

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, () -> begin
        let data = abilityCond(DARK_SUMMONING_NL, DARK_SUMMONING_L, DARK_SUMMONING_S)
        if data.check
            let caster = EventData.getSpellAbilityUnit()
            let owner = caster.getOwner()
            let vase = CreateGroup()

            real delay
            if data.isNL
                delay = DELAY_NL[data.lvl]
            else
                delay = DELAY_L[data.lvl]

            let where = THE_DRAENEI.has(owner)? actDrSpawn : actDeSpawn
            forUnitsInRange(EventData.getSpellTargetPos(), AREA, u -> begin
                if owner.isAllyOf(u.getOwner()) and u.isType(UNIT_TYPE_HERO)
                    u.addEffect(Abilities.darkSummonTarget, "origin").destroyAfter(delay)
                    vase.add(u)
            end)
            doAfter(delay) -> 
                for u from vase
                    let pos = where.randomPoint()
                    let pos3 = pos.withZ(50.)
                    let myeffect = addEffect(Abilities.darkSummonTarget, pos)
                    let selected = u.isSelected(u.getOwner())
                    let m = new Missiles(u.getPos3with(50.), pos3)
                    m.setModel(Abilities.darkSummonMissile)
                    m.source = u
                    m.setArc(10..asAngleDegrees())
                    m.setSpeed(1500.)
                    m.onFinish(() -> begin
                        m.source.setPos(pos)
                        m.source.show()
                        m.source.pause(false)
                        if selected
                            m.source.getOwner().selectSingle(m.source)
                        myeffect.destr()
                        SOUND_2.playOnPoint(pos3)
                        return true
                    end)
                    m.launch()
                    u.hide()
                    SOUND_1.playOnPoint(pos3)
                    u.pause(true)
                vase.destr()
    end)

function ChannelAbilityPreset.general()
    this
        ..presetOption(Option.TARGETIMAGE, true)
        ..presetBaseOrderID(lvl -> "inferno")
        ..presetFollowThroughTime(lvl -> 0.75)
        ..tooltipStartListen()
        ..setHotkey("W")
        ..setName("Dark Summoning")
        ..presetCastRange(lvl -> 99999.)
        ..presetTargetTypes(Targettype.POINT)
        ..presetAreaofEffect(lvl -> AREA)
        ..presetManaCost(lvl -> MANA)
        ..presetIcon(Icons.bTNDarkSummoning)
        ..setAnimationNames("spell")

@compiletime function gen()
    new ChannelAbilityPreset(DARK_SUMMONING_NL, 2, true, new AbilityTooltipGenerator("Summon ally heros at the base."))
        ..setHeroAbility(false)
        ..general()
        // Stats
        ..addTooltipProperty("Delay", lvl -> DELAY_NL[lvl-1])
        ..presetCooldown(lvl -> CD_NL[lvl-1])
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        ..setEditorSuffix("(Not leveling)")
    
    new ChannelAbilityPreset(DARK_SUMMONING_L, 4, true, new AbilityTooltipGenerator("Summon ally heros at the base."))
        ..setHeroAbility(true)
        ..setRequiredLevel(1)
        ..general()
        // Stats
        ..addTooltipProperty("Delay", lvl -> DELAY_L[lvl-1])
        ..presetCooldown(lvl -> CD_L[lvl-1])
        // Audiovisual
        // Tooltip
        ..tooltipStopListen()
        .setEditorSuffix("(Leveling)")

    new ChannelAbilityPreset(DARK_SUMMONING_S, 6, true, new AbilityTooltipGenerator("Summon ally heros at the base."))
        ..setHeroAbility(false)
        ..general()
        ..presetTooltipNormal(lvl -> "[|cffffcc00F|r] Dark Summoning")
        ..setHotkey("F")
        // Stats
        ..presetCooldown(lvl -> lvl < 3 ? CD_NL[lvl-1]: CD_L[lvl-3])
        ..presetTooltipNormalExtended(lvl -> "Summon ally heros at the base.")
        ..setEditorSuffix("(Stealed)")