package Generators

import Functions
import initlater Missions
import PlayerData
import Towers
import DrAndDeSpawn
import GeneratedValues

import ObjectIdGenerator
import UnitObjEditing

public unit generator1 = null
public unit generator2 = null
public vec2 generatorPos1 = Rects.generator1.getCenter()
public vec2 generatorPos2 = Rects.generator2.getCenter()
public boolean generatorsDestroyed
public int generatorsLeft
public trigger destroyGenerators = CreateTrigger()

let GENERATOR = compiletime(UNIT_ID_GEN.next())

init
    onReset(true, () -> begin
        if generator1 == null
            generator1 = createUnit(DEMON, GENERATOR, generatorPos1, UNIT_FACING)
        if generator2 == null
            generator2 = createUnit(DEMON, GENERATOR, generatorPos2, UNIT_FACING)
        generator1.setInvulnerable(true)
        generator2.setInvulnerable(true)
        generatorsDestroyed = false
        generatorsLeft = 2
    end)
    onReset(false, () -> begin
        for i = 0 to 7
            Dests.entranceForceWall[i].close()
    end)

    destroyGenerators
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
        ..addCondition(Condition(() -> GetDyingUnit().getTypeId() == GENERATOR))
        .addAction(() -> begin
            generatorsLeft--
            if GetDyingUnit() == generator1
                generator1 = null
            else if GetDyingUnit() == generator2
                generator2 = null
            
            if generatorsLeft == 1
                QuestMessageBJ(bj_FORCE_ALL_PLAYERS, bj_QUESTMESSAGE_UPDATED, "A generator was destroyed.")
            else if generatorsLeft == 0
                generatorsDestroyed = true
                forUnitsInRect(bj_mapInitialPlayableArea, u -> begin
                    if u.getTypeId() == ADVANCE_DEATH_TOWER
                        u.setOwner(players[PLAYER_NEUTRAL_PASSIVE], false)
                    wallGenerator.kill()
                    for i = 0 to 7
                        Dests.entranceForceWall[i].open()
                end)
                startMission(5)
        end)

@compiletime function gen()
    new UnitDefinition(GENERATOR, 'npgr')
        ..setHitPointsMaximumBase(1000)
        ..setHitPointsRegenerationType("always")
        ..setHitPointsRegenerationRate(1.)