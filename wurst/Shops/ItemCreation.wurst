package ItemCreation

import public ObjectIdGenerator
import public UnitObjEditing
import public ItemObjEditing
import public UnitIds
import public Assets
import public ObjectIds
import public Annotations
import ClosureEvents
import HashMap
import HashList
import Orders

import Functions
import public Imports
import public ItemSpells

import public RecipeItem
import public MoveSpeedX

public function fixRecipe(Recipe recipe, RecipeItem result, int goldCost, int woodCost)
    recipe.generateTooltip(
        recipe.tooltipExtended +
        "|cff6d6d6dTotal cost: " +
        (goldCost > 0 ? goldCost.toString() + " gold and " : "") +
        (woodCost > 0 ? woodCost.toString() + " lumber." : "") + "|r")
    
    result..setGoldCost(goldCost)..setLumberCost(woodCost)

/** Class created to have the recipe item in the shop with other icon. */
public class Recipe extends RecipeItem

    private int dummyItem
    private ItemDefinition dummyDef

    construct()
        super('skrt')
        
        //setArmorType(ArmorSoundType.Ethereal)
        setClassification("Campaign")
    
        dummyItem = ITEM_ID_GEN.next()

        dummyDef = new ItemDefinition(dummyItem, 'skrt')
    
    override function setStockReplenishInterval(int data)
        dummyDef.setStockReplenishInterval(data)
    
    override function setButtonPositionX(int data)
        dummyDef.setButtonPositionX(data)
    
    override function setButtonPositionY(int data)
        dummyDef.setButtonPositionY(data)
    
    override function setHotkey(string data)
        dummyDef.setHotkey(data)
    
    override function setTooltipExtended(string data)
        super.setTooltipExtended(data)
        dummyDef.setTooltipExtended(data)
    
    override function setTooltipBasic(string data)
        dummyDef.setTooltipBasic(data)
    
    /** Always use this function at the end. */
    override function setName(string data)
        super.setName(data)

        dummyDef
            ..setGoldCost(goldCost)
            ..setLumberCost(lumberCost)
            ..setInterfaceIcon(icon)
        
        setInterfaceIcon(Icons.bTNScrollOfRegeneration)
    
    function getDummyItem() returns int
        return dummyItem

    function initShopConditions()
        EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM, () -> begin
            let m = EventData.getManipulatedItem()
            if m.getTypeId() == dummyItem
                m.remove()
                EventData.getTriggerUnit().addItemById(newId)
        end)

let fakeBooks = new HashMap<int, int>()
let realBooks = new HashList<int>()

public class FakeBook extends ItemDefinition
    int tomeId
    ItemDefinition tomeDef

    construct(int origId)
        super(ITEM_ID_GEN.next(), origId)
        tomeId = ITEM_ID_GEN.next()
        tomeDef = new ItemDefinition(tomeId, 'manh')..setAbilities("")
    
    override function setInterfaceIcon(string data)
        super.setInterfaceIcon(data)
        tomeDef.setInterfaceIcon(data)
    
    override function setTooltipBasic(string data)
        super.setTooltipBasic(data)
        tomeDef.setTooltipBasic(data)
    
    override function setTooltipExtended(string data)
        super.setTooltipExtended(data)
        tomeDef.setTooltipExtended(data)
    
    override function setGoldCost(int data)
        super.setGoldCost(data)
        tomeDef.setGoldCost(data)
    
    override function setLumberCost(int data)
        super.setLumberCost(data)
        tomeDef.setLumberCost(data)
    
    override function setStockStartDelay(int data)
        //super.setStockStartDelay(data)
        tomeDef.setStockStartDelay(data)
    
    override function setButtonPositionX(int data)
        //super.setButtonPositionX(data)
        tomeDef.setButtonPositionX(data)
    
    override function setButtonPositionY(int data)
        //super.setButtonPositionY(data)
        tomeDef.setButtonPositionY(data)
    
    override function setHotkey(string data)
        //super.setHotkey(data)
        tomeDef.setHotkey(data)
    
    function getDummyItem() returns int
        return tomeId

    function register()
        fakeBooks.put(tomeId, newId)
        realBooks.add(newId)

init
    EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM, () -> begin
        let m = GetManipulatedItem()
        let id = m.getTypeId()
        if fakeBooks.has(id)
            let u = GetManipulatingUnit()
            if u.isType(UNIT_TYPE_HERO)
                u.addItemById(fakeBooks.get(id))
            else
                createItem(fakeBooks.get(id), u.getPos())
            m.remove()
    end)
    EventListener.add(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, () -> begin
        if realBooks.has(GetOrderTargetItem().getTypeId())
            let u = GetOrderedUnit()
            if not u.isType(UNIT_TYPE_HERO)
                u.issueTargetOrderById(Orders.attack, u)
                message("error", "This unit can't use powerups.", u.getOwner())
    end)