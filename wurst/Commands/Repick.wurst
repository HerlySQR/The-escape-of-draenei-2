package Repick

import ModeSelection
import HeroSelection
import MyBoard
import Reset
import GeneratedValues
import Barrels

import Icons
import DamageEvent
import ClosureTimers

public boolean yesOrYes = false

int array inCombat
boolean canRepick = true
DamageListener noRepick
trigger reduce = CreateTrigger()

public function enableRepick()
    canRepick = true
    DamageEvent.addListener(noRepick)
    reduce.enable()

public function disableRepick()
    canRepick = false
    DamageEvent.removeListener(noRepick)
    reduce.disable()

public function repick(player p)
    if canRepick or yesOrYes
        let i = p.getNumber()
        let repick = hero[i]

        if repick != null
            if inCombat[i] > 0 and not yesOrYes
                DisplayTextToPlayer(p, 0., 0., "You cannot repick during combat")
                return
            if whatMode[5] == LEVELING
                heroModel.get(repick.getTypeId()).show()
            // TODO udg_Si_lo_mataste[p.getId()] = false
            hero[i] = null
            table.setItemIcon(1, row[i], Icons.bTNSelectHeroOn)
            if THE_DRAENEI.has(p)
                repick.getItemOfId(BARREL).remove()
                repick.setPos(Rects.selectorDraenei.getCenter())
            else
                repick.setPos(Rects.selectorDemon.getCenter())
            p.panCamToTimed(repick, 0.)
            let pos = circleRegion[i].getCenter()
            for j = 0 to 5
                repick.itemInSlot(j).setPos(pos)
            /* TODO
            if IsPlayerInForce(p,udg_Ya_te_vi[0])
                ForceAddPlayer(udg_Sabe_quien_eres[0],p)
            for j = 1 to 6
                if IsPlayerInForce(p,udg_Mision[j]) or IsPlayerInForce(p,udg_No_aceptaste_la_mision[j])
                    ForceAddPlayer(udg_Sabe_quien_eres[j],p)*/
            canPick.add(p)
            repick.remove()

// No during combat
init
    onModesSelected(() -> begin
        if whatMode[5] == LEVELING
            doAfter(20, () -> begin
                print("\nYou can only repick until 2 minutes.")
                bj_questHintSound.play()
            end)

            doAfter(120, () -> begin
                print("\n|cffff0000You cannot longer repick.|r")
                disableRepick()
            end)
    end)

    onReset(false, () -> begin
        enableRepick()
    end)

    noRepick = DamageEvent.addListener(() -> begin
        if DamageEvent.getTarget().isType(UNIT_TYPE_HERO)
            inCombat[DamageEvent.getTarget().getOwner().getNumber()] = 3
    end)
    reduce
        ..registerTimerEvent(1, true)
        .addAction(() -> begin
            for i = 1 to 10
                inCombat[i]--
        end)
