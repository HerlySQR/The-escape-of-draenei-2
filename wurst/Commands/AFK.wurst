package AFK

import MyBoard
import ChatEnter

public constant whoAreHere = CreateForce()

let gray = colorA(128, 128, 128, 255)

int array afkTime

init
    onChatCommand("afk", (p, _) -> begin
        for i = 1 to 10
            if afkTime[i] >= 30
                printTimedToPlayer(playerName[i] + " is " + afkTime[i].toString() + " seconds afk.", 10., p)
    end)
        
    // To see you are here
    let t = CreateTrigger()
    for i = 1 to 11
        if i != 6
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_SELECTED, null)
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_DESELECTED, null)
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_ISSUED_ORDER, null)
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER, null)
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, null)
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_PAWN_ITEM, null)
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_PICKUP_ITEM, null)
            t.registerPlayerUnitEvent(players[i], EVENT_PLAYER_UNIT_SELL_ITEM, null)
            t.registerPlayerChatEvent(players[i], "", false)
            t.registerPlayerEvent(players[i], EVENT_PLAYER_END_CINEMATIC)
            t.registerPlayerEvent(players[i], EVENT_PLAYER_ALLIANCE_CHANGED)
    t.addAction(() -> begin
        let p = GetTriggerPlayer()
        let i = p.getNumber()
        afkTime[i] = 0
        if not whoAreHere.has(p)
            whoAreHere.add(p)
            for j = 0 to 4
                gameBoard.setItemValueColor(row[i], j, COLOR_WHITE)
            gameBoard.setItemValue(row[i], 1, playerName[i])
    end)
    // Register
    CreateTimer().startPeriodic(1., () -> begin
        for i = 1 to 11
            let j = players[i].getNumber()
            if players[i].isIngame()
                afkTime[j]++
            if afkTime[j] >= 30
                whoAreHere.remove(players[i])
                gameBoard.setItemValue(row[j], 1, players[i].getName())
                for k = 0 to 4
                    gameBoard.setItemValueColor(row[j], k, gray)
    end)