/** The player only will select the revivng hero if don't have a selected unit controlled by him*/
package HaveAUnitSelected

import ClosureForGroups
import Reference

function player.hasUnitSelected() returns boolean
    let amount = new Reference(0)
    SyncSelections()
    forUnitsSelected(this, u -> begin
        if u.isAlive() and (u.getOwner() == this or u.getOwner().getPlayerAlliance(this, ALLIANCE_SHARED_CONTROL))
            amount.val++
    end)
    return amount.into() > 0

init
    // When a hero revives
    CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_HERO_REVIVE_FINISH)
        ..addAction(() -> begin
            let h = GetRevivingUnit()
            let p = h.getOwner()
            if not p.hasUnitSelected()
                p.selectSingle(h)
                p.panCamToTimed(h, 0.)
            else
                DisplayTextToPlayer(p, 0, 0, "|cff61dbffYour hero has resurrected|r")
        end)