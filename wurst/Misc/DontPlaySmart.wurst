package DontPlaySmart

import DamageEvent
import PlayerData
import GeneratedValues

import Orders
import ClosureForGroups
import ClosureTimers

// Friendly Fire off
init
    CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ATTACKED)
        ..addAction(() -> begin
            if GetAttacker().getOwner().isAllyOf(GetTriggerUnit().getOwner()) and GetUnitLifePercent(GetTriggerUnit()) >= 30.
                GetAttacker().issueTargetOrderById(Orders.smart, GetTriggerUnit())
        end)

// Don't try pass

public trigger DONT_PASS = CreateTrigger()

init
    DONT_PASS
        ..registerTimerEvent(1., true)
        ..addAction(() -> begin
            forUnitsInRect(Rects.DONT_PASS, u -> begin
                u.setInvulnerable(false)
                u.removeAbility('BHbn')
            end)
        end)

// It's not too easy destroy the generator

let theyNow = CreateForce()

init
    DamageEvent.addListener(() -> begin
        if DamageEvent.getSource().hasAbility('Bvul') and DamageEvent.getTarget().getTypeId() == 'npgr'
            let u = DamageEvent.getSource()
            let p = u.getOwner()
            if not theyNow.has(p)
                theyNow.add(p)
                DisplayTextToPlayer(u.getOwner(), 0., 0., "To prevent injustice, the buff of invunerability will be remove on 2 seconds.")
                if p == localPlayer
                    bj_questHintSound.play()
            doAfter(2.) -> 
                u.removeAbility('Bvul')
    end)

// TODO: Courier, mission items

// You will lose if you do this

function cond() returns boolean
    let d = GetOrderTargetDestructable()
    if not (d == Dests.dungeonExitDoor1 or d == Dests.dungeonExitDoor2 or d == Dests.mainEntranceDoor)
        return false
    else if not THE_DEMONS.has(GetOrderedUnit().getOwner())
        return false
    else if not (GetIssuedOrderId() == Orders.attack or GetIssuedOrderId() == Orders.smart)
        return false
    return true

init
    CreateTrigger()
        ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER)
        ..addCondition(Condition(function cond))
        ..addAction(() -> GetOrderedUnit().issuePointOrderById(Orders.move, GetOrderTargetDestructable().getPos()))

