package MyBoard

import ClosureTimers
import Icons

import public Functions
import public PlayerData
import ModeSelection
import HeroType
import ReviveN
import DrAndDeSpawn

public constant gameBoard = CreateMultiboard()

init
    onModesSelected(() -> playersOrder())

    nullTimer() -> 
        var pl = 3
        for i = 0 to bj_MAX_PLAYERS
            if players[i].isIngame()
                pl++
        
        gameBoard
            ..setRowCount(pl)
            ..setColumnCount(5)

        for i = 0 to 4
            gameBoard.getItem(0, i)
                ..setStyle(true, false)
                ..setValueColor(COLOR_GOLD)
        
        for i = 0 to pl - 1
            gameBoard.setItemWidth(i, 0, 0.0145)
            gameBoard.setItemWidth(i, 1, 0.0650)

        gameBoard.getItem(1, 0).setStyle(true, false)
        gameBoard.getItem(1, 1).setStyle(true, false)

        gameBoard
            ..setTitle("Statistics")
            ..display(false)
            ..setItemValue(0, 0, "")
            ..setItemValue(1, 0, ("Drae").color(colorId[11]))
            ..setItemValue(1, 1, (" nei").color(colorId[11]))
            ..setItemValue(0, 1, "Player")
            ..setItemValue(0, 2, "Kills")
            ..setItemValue(0, 3, "Deaths")
            ..setItemValue(0, 4, "Assists")

public function playersOrder()
    for i = 1 to 12
        row[i] = 999

    var t = 2
    for i = 1 to 5
        if playerM(i).isIngame()
            row[i] = t
            t++

    let d = t
    t++

    for i = 6 to 10
        if playerM(i).isIngame()
            row[i] = t
            t++
    
    gameBoard.setRowCount(t)
    for i = 0 to gameBoard.getRowCount() - 1
        for j = 0 to gameBoard.getColumnCount() - 1
            gameBoard.getItem(i, j).setStyle(true, false)

    for i = 1 to 10
        let r = row[i]
        if r != 999
            gameBoard
                ..setItemValue(r, 1, playerName[i])
                ..setItemValue(r, 2, kills[i].toString())
                ..setItemValue(r, 3, deaths[i].toString())
                ..setItemValue(r, 4, assists[i].toString())
            gameBoard.getItem(r, 0).setStyle(false, true)
            let g = HeroType.get(hero[i])
            if g == null
                gameBoard.setItemIcon(r, 0, Icons.bTNSelectHeroOff)
            else
                gameBoard.setItemIcon(r, 0, g.getIcon())
    
    gameBoard.getItem(d, 0).setStyle(true, false)
    gameBoard.getItem(d, 1).setStyle(true, false)
    gameBoard
        ..setItemValue(d, 0, ("Dem").color(colorId[12]))
        ..setItemValue(d, 1, (" ons").color(colorId[12]))
    for i = 2 to 4
        gameBoard.setItemValue(d, i, "")
    
    gameBoard..show()..maximize()

// Game time
var seconds = 0
var minutes = 0
var hours = 0

init
    doPeriodically(1, cb -> begin
        seconds++
        if seconds == 60
            seconds = 0
            minutes++
        if minutes == 60
            minutes = 0
            hours++
        gameBoard.setTitle("Statistics  | |  " + (hours < 10 ? "0" : "") + hours.toString() + ":" + (minutes < 10 ? "0" : "") + minutes.toString() + ":"  + (seconds < 10 ? "0" : "") + seconds.toString())
        // Triggers or timers to enable
        if reviveNRun
            if seconds == 0 or seconds == 20 or seconds == 40
                reviveNRun = false
                reviveStart()
        if spawnRun
            if seconds == 0 or seconds == 30
                spawnRun = false
                startSpawn()
    end) 
    