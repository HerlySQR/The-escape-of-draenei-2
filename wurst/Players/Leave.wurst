package Leave

import MyBoard
import AFK
import SummonCourier

import SoundUtils
import ClosureForGroups

let SOUND = new SoundDefinition(Sounds.upkeepRing, false, false)

init
    CreateTrigger()
        ..registerPlayerEvent(players[1], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[2], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[3], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[4], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[5], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[7], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[8], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[9], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[10], EVENT_PLAYER_LEAVE)
        ..registerPlayerEvent(players[11], EVENT_PLAYER_LEAVE)
        ..addAction(() -> begin
            let p = GetTriggerPlayer()
            let temp = p.getNumber()
            let pos = circleRegion[temp].getCenter()

            whoAreHere.remove(p)
            printTimed(playerName[temp] + " |cffff0000left the game.|r", 10.)

            for i = 0 to 5
                hero[temp].itemInSlot(i).setPos(pos)
            hero[temp].remove()

            forUnitsOfPlayer(p, u -> begin
                if u.getTypeId() == COURIER
                    for m in u.inventory()
                        m.setPos(pos)
            end)

            forItemsInRect(bj_mapInitialPlayableArea, m -> begin
                if GetItemPlayer(m) == p
                    m.setPos(pos)
                    SetItemPlayer(m, players[PLAYER_NEUTRAL_PASSIVE], false)
            end)
            SOUND.play()
            playersOrder()
        end)